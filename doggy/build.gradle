import org.febit.boot.devkit.jooq.gradle.container.DbType

plugins {
  alias libs.plugins.spring.boot
  alias libs.plugins.febit.boot.jooq
  alias libs.plugins.febit.boot.flyway
}

def PKG = "org.febit.boot.demo.doggy"

springBoot {
  mainClass.set "${PKG}.DoggyApplication"
}

bootRun {
  systemProperties System.properties
}

bootJar {
  archiveFileName = "${project.name}.jar"
}

codegenModule {
  module "${PKG}.DoggyApiVersion"
  // gitDir = null
}

febitFlyway {
  excludeTask '.*-(baseline|undo|repair)-.*'
  excludeTask '.*-(prod|pre|uat|unit-test)'
}

codegenJooq {
  def MODEL_PKG = "${PKG}.model"

  containerDatabase {
    type = DbType.POSTGRES
  }

//  embeddedPostgres()
//  presetJdbc {
//    url = "jdbc:postgresql://localhost:54322/demo_dev?currentSchema=doggy"
//    user = "test"
//    password = "test"
//  }

  schema = 'doggy'
  targetPackage = "${PKG}.jmodel"
  generate = {
    springAnnotations = true
  }
  forcedTypes {
    timeToInstant()
    mapping([
        t_doggy: [
            breed : "enum: ${MODEL_PKG}.doggy.DoggyBreed",
            gender: "enum: ${MODEL_PKG}.doggy.DoggyGender",
        ]
    ])
  }
}

dependencies {
  implementation 'org.febit:febit-lang'
  implementation 'org.febit:febit-commons-stuff'

  implementation 'org.febit.boot:febit-boot-starter-webmvc'
  implementation 'org.febit.boot:febit-boot-starter-jooq'
  implementation 'org.febit.boot:febit-boot-starter-jwt'

  implementation 'org.springframework.security:spring-security-crypto'
  implementation 'org.springframework.boot:spring-boot-starter-actuator'

  runtimeOnly 'org.flywaydb:flyway-core'
  runtimeOnly 'org.flywaydb:flyway-database-postgresql'
  runtimeOnly 'com.h2database:h2'
  runtimeOnly 'org.postgresql:postgresql'

  codegenJooq 'org.slf4j:slf4j-simple'
  codegenJooq 'org.postgresql:postgresql'

  testRuntimeOnly 'com.h2database:h2'
  testImplementation 'org.springframework.boot:spring-boot-starter-test'
}
